/*
 * Copyright (c) 2011-2013 GoPivotal, Inc. All Rights Reserved.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *       http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
description = 'Reactor Data components'

ext {
	gradleVersion = '1.7'
	gradleScriptDir = "${rootProject.projectDir}/gradle"

	// Logging
	slf4jVersion = '1.7.5'
	logbackVersion = '1.0.13'

	// Spring
	springVersion = '4.0.0.M3'
	sdCommonsVersion = '1.6.1.RELEASE'
	sdMongoVersion = '1.3.1.RELEASE'
	sdCouchbaseVersion = '1.0.0.M1'

	// Reactor
	reactorVersion = '1.0.0.RC1'

	// Libraries
	lettuceVersion = '2.3.3'
	jacksonVersion = '2.2.3'
}

buildscript {
	repositories {
		maven { url "http://repo.spring.io/plugins-release" }
	}
	dependencies {
		classpath 'org.springframework.build.gradle:propdeps-plugin:0.0.4'
	}
}
apply from: "$gradleScriptDir/setup.gradle"

allprojects {
	apply plugin: 'propdeps'
	apply plugin: 'java'
	apply plugin: 'idea'
	apply plugin: 'eclipse'

	configurations.all {
		exclude group: "commons-logging"
	}

	[compileJava, compileTestJava]*.options*.compilerArgs = [
			"-Xlint:serial",
			"-Xlint:varargs",
			"-Xlint:cast",
			"-Xlint:classfile",
			"-Xlint:dep-ann",
			"-Xlint:divzero",
			"-Xlint:empty",
			"-Xlint:finally",
			"-Xlint:overrides",
			"-Xlint:path",
			"-Xlint:processing",
			"-Xlint:static",
			"-Xlint:try",
			"-Xlint:-options", // intentionally disabled
			"-Xlint:-fallthrough", // intentionally disabled
			"-Xlint:-rawtypes", // TODO enable and fix warnings
			"-Xlint:-deprecation", // TODO enable and fix warnings
			"-Xlint:-unchecked" // TODO enable and fix warnings
	]

	compileJava {
		sourceCompatibility = 1.7
		targetCompatibility = 1.7
	}

	compileTestJava {
		sourceCompatibility = 1.7
		targetCompatibility = 1.7
	}

	test {
		systemProperty("java.awt.headless", "true")
		systemProperty("testGroups", project.properties.get("testGroups"))
		scanForTestClasses = false
		include '**/*Tests.*'
		exclude '**/*Abstract*.*'
	}

	group = 'org.projectreactor.data'
	repositories {
		//mavenLocal()
		maven { url 'http://repo.spring.io/libs-milestone' }
		maven { url 'http://files.couchbase.com/maven2' }
		mavenCentral()
	}

	dependencies {
		// Logging
		compile "org.slf4j:slf4j-api:$slf4jVersion"

		// Testing
		testCompile "junit:junit-dep:4.11",
				"org.hamcrest:hamcrest-library:1.3",
				"org.springframework:spring-test:$springVersion"
		testRuntime "ch.qos.logback:logback-classic:$logbackVersion"
	}
}

project('data-core') {
	description = 'Core Reactor components for working with datastores'
	dependencies {
		// Reactor
		compile "org.projectreactor:reactor-core:$reactorVersion"
	}
}

project('data-redis') {
	description = 'Reactor components for working with Spring Data Repositories'
	dependencies {
		compile project(':data-core')

		// Lettuce
		compile "com.lambdaworks:lettuce:$lettuceVersion"

		// Jackson
		compile "com.fasterxml.jackson.core:jackson-databind:$jacksonVersion"
	}
}

project('data-spring') {
	description = 'Reactor components for working with Spring Data Repositories'
	dependencies {
		compile project(':data-core'),
				project(':data-redis')

		// Spring
		compile "org.springframework:spring-beans:$springVersion",
				"org.springframework:spring-context:$springVersion",
				"org.springframework:spring-context-support:$springVersion",
				"org.springframework:spring-core:$springVersion",
				"org.springframework:spring-expression:$springVersion",
				"org.springframework:spring-tx:$springVersion",
				"org.springframework:spring-web:$springVersion"

		// Spring Data
		compile "org.springframework.data:spring-data-commons:$sdCommonsVersion"

		// Reactor
		compile "org.projectreactor:reactor-spring:$reactorVersion"

		// Lettuce
		provided "com.lambdaworks:lettuce:$lettuceVersion"

		// Testing
		testCompile "org.springframework.data:spring-data-mongodb:$sdMongoVersion",
				"org.springframework.data:spring-data-couchbase:$sdCouchbaseVersion",
				"com.lambdaworks:lettuce:$lettuceVersion"
		testRuntime "org.slf4j:jcl-over-slf4j:$slf4jVersion"
	}
}
